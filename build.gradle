group 'geoand'
version '0.1'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5:indy'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'org.jgrapht:jgrapht-core:0.9.1'
    compile 'com.googlecode.combinatoricslib:combinatoricslib:2.1'
    compile 'org.apache.commons:commons-math3:3.6'
    compile 'com.google.guava:guava:19.0'
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'org.assertj:assertj-core:3.3.0'
}

task runScript(type: JavaExec) {
    description 'Run Groovy script'

    // Set main property to name of Groovy script class.
    main = "geoand.adventofcode.problems.Problem${project.hasProperty('number') ? number : 1}"

    // Set classpath for running the Groovy script.
    classpath = sourceSets.main.runtimeClasspath
}

defaultTasks 'runScript'

sourceSets {
    main {
        java { srcDirs = [] }    // no source dirs for the java compiler
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }  // compile   everything in src/ with groovy
    }
}
